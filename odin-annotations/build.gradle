apply plugin: 'java'

apply from: '../scripts/gradle/publishing.gradle'

dependencies {
	compile project(':odin-core')

	compile 'javax.inject:javax.inject:1'

	testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.1'
	testCompile 'org.junit.jupiter:junit-jupiter-params:5.3.1'
	testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
	testCompile 'org.junit.platform:junit-platform-launcher:1.3.1'
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

javadoc {
	title "Odin Annotations"
	options {
		bottom "Copyright \u00a9 2018, 2019 JCoder Ltd."
	}
}

task sourceJar(type: Jar) {
	classifier "sources"
	from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier "javadoc"
	from javadoc.destinationDir
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			def artifactName = 'odin-annotations'
			project.ext.customizePom(pom, artifactName)
			
			groupId 'io.jcoder.odin'
			artifactId artifactName
			version project.version
			
			from components.java

			artifact(sourceJar) {
				classifier = 'sources'
			}
			artifact(javadocJar) {
				classifier = 'javadoc'
			}
		}
	}
}

if (project.hasProperty('signing.keyId')) {
	signing {
	    sign publishing.publications.mavenJava
	}
}
