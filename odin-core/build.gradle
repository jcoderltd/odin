apply plugin: 'java'

apply from: '../scripts/gradle/publishing.gradle'

dependencies {
	compile 'javax.servlet:javax.servlet-api:3.1.0'
	
	compile 'javax.inject:javax.inject:1'
	
	compile 'javax.annotation:javax.annotation-api:1.3.2'
	
	compile 'org.slf4j:slf4j-api:1.7.26'
	
	testCompile 'org.junit.jupiter:junit-jupiter-api:5.4.0'
	testCompile 'org.junit.jupiter:junit-jupiter-params:5.4.0'
	testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.4.0'
	testCompile 'org.junit.platform:junit-platform-launcher:1.4.0'

	testRuntime 'ch.qos.logback:logback-classic:1.3.0-alpha4'
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}

javadoc {
	title "Odin Core"
	options {
		bottom "Copyright \u00a9 2018, 2019 JCoder Ltd."
	}
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

task sourceJar(type: Jar) {
	classifier "sources"
	from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier "javadoc"
	from javadoc.destinationDir
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			def artifactName = 'odin-core'
			project.ext.customizePom(pom, artifactName)
			
			groupId 'io.jcoder.odin'
			artifactId artifactName
			version project.version
			
			from components.java

			artifact(sourceJar) {
				classifier = 'sources'
			}
			artifact(javadocJar) {
				classifier = 'javadoc'
			}
		}
	}
}

if (project.hasProperty('signing.keyId')) {
	signing {
	    sign publishing.publications.mavenJava
	}
}